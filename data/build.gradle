apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def env = new Properties()
if (rootProject.file("env.properties").exists())
    env.load(new FileInputStream(rootProject.file("env.properties")))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField("String", "BaseURL", "\"${env['baseUrl']}\"")
            buildConfigField("String", "TokenTMDb", "\"${env['apiKeyTMBb']}\"")
            buildConfigField("String", "BaseUrlImage", "\"${env['baseUrlImage']}\"")
        }

        develop {
            minifyEnabled false
            debuggable true

            buildConfigField("String", "BaseURL", "\"${env['baseUrlDevelop']}\"")
            buildConfigField("String", "TokenTMDb", "\"${env['apiKeyTMBb']}\"")
            buildConfigField("String", "BaseUrlImage", "\"${env['baseUrlImage']}\"")
        }
        qa {
            minifyEnabled false
            debuggable true

            buildConfigField("String", "BaseURL", "\"${env['baseUrlQA']}\"")
            buildConfigField("String", "TokenTMDb", "\"${env['apiKeyTMBb']}\"")
            buildConfigField("String", "BaseUrlImage", "\"${env['baseUrlImage']}\"")
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'debug')
            setIgnore(true)
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation project(":domain")

    api dataDependencies.values()
    kapt rootProject.ext.roomDependencies.roomCompiler
    testImplementation dataTestDependencies.values()
}
