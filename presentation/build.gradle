apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def env = new Properties()
if (rootProject.file("env.properties").exists())
    env.load(new FileInputStream(rootProject.file("env.properties")))

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.1"

    testBuildType "develop"

    defaultConfig {
        applicationId "com.jsevilla.movieviewer"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        develop {
            keyAlias env['keyAliasDevelop']
            keyPassword env['keyPasswordDevelop']
            storeFile file(env['storeFileDevelop'])
            storePassword env['storePasswordDevelop']
        }

        release {
            keyAlias env['keyAliasRelease']
            keyPassword env['keyPasswordRelease']
            storeFile file(env['storeFileRelease'])
            storePassword env['storePasswordRelease']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }

        develop {
            applicationIdSuffix ".develop"
            versionNameSuffix "-develop"
            debuggable true

            signingConfig signingConfigs.develop
        }
        qa {
            applicationIdSuffix ".qa"
            versionNameSuffix "-qa"
            debuggable true

            signingConfig signingConfigs.develop
        }
    }

    variantFilter { variant ->
        if (variant.buildType.name == 'debug')
            setIgnore(true)
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation project(":domain")
    implementation project(":data")

    implementation presentationDependencies.values()
    kapt rootProject.ext.presentationDependenciesKapt.glideCompiler
    testImplementation presentationTestDependencies.values()
    androidTestImplementation presentationTestImplementationDependencies.androidXRunner
    androidTestImplementation presentationTestImplementationDependencies.androidXEspresso
}
